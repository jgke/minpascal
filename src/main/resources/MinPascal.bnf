S ::= Program
Program ::= program identifier semicolon Declaration* Block dot
Block ::= begin Statement (semicolon [Statement])* end
Statement ::= SimpleStatement | StructuredStatement | Declaration

// read and write are handled in the compiler as "normal" function calls
SimpleStatement ::= ReturnStatement
                    | AssertStatement
                    | ReadStatement
                    | IdentifierStatement
IdentifierStatement ::= !read identifier IdentifierStatementContent
IdentifierStatementContent ::= AssignmentStatement | Arguments
ReadStatement ::= read op Variable (comma Variable)* cp
ReturnStatement ::= return Expression
AssertStatement ::= assert op Expression cp
AssignmentStatement ::= [ob Expression cb] assign Expression

StructuredStatement ::= Block | If | While
If ::= if Expression then Statement [else Statement]
While ::= while Expression do Statement

Declaration ::= VarDeclaration | ProcedureDeclaration | FunctionDeclaration
VarDeclaration ::= var identifier (comma identifier)* colon Type
Type ::= SimpleType | ArrayType
SimpleType ::= int | str | real | boolean
ArrayType ::= array ob [Expression] cb of SimpleType
ProcedureDeclaration ::= procedure identifier Parameters semicolon Block
FunctionDeclaration ::= function identifier Parameters colon Type semicolon Block
Parameters ::= op [Parameter (comma Parameter)*] cp
Parameter ::= [var] identifier colon Type

Arguments ::= op [Expression (comma Expression)*] cp
Expression ::= SimpleExpression [RelOp SimpleExpression]
SimpleExpression ::= [Sign] Term [AddOp Term]
Term ::= Factor [MulOp Factor]
Factor ::= SubFactor [SizeExpression]
SubFactor ::= Variable [Arguments]
             | Literal
             | op Expression cp
             | not Factor
Variable ::= identifier [ob Expression cb]
SizeExpression ::= dot size
// 'true' and 'false' are treated as variables
Literal ::= realliteral | integerliteral | stringliteral

RelOp ::= notequals | equals | lessthanequals | lessthan | morethanequals | morethan
Sign ::= plus | minus
AddOp ::= plus | minus | or
MulOp ::= times | divide | mod | and

and ::= 'and'
array ::= 'array'
assert ::= 'assert'
assign ::= ':='
begin ::= 'begin'
boolean ::= 'boolean'
cb ::= ']'
colon ::= ':'
comma ::= ','
cp ::= ')'
divide ::= '/'
do ::= 'do'
dot ::= '.'
else ::= 'else'
end ::= 'end'
equals ::= '=='
function ::= 'function'
identifier ::= !keyword identifierregex
identifierregex ::= "[a-zA-Z][a-zA-Z0-9]*"
keyword ::= or | and | not | if | then | else | of | while | do
          | begin | end | var | array | procedure | function
          | program | assert | return
if ::= 'if'
integerliteral ::= "[0-9]+"
lessthan ::= '<'
lessthanequals ::= '<='
minus ::= '-'
mod ::= '%'
morethan ::= '>'
morethanequals ::= '>='
not ::= '!'
notequals ::= '<>'
ob ::= '['
of ::= 'of'
op ::= '('
or ::= 'or'
plus ::= '+'
procedure ::= 'procedure'
program ::= 'program'
read ::= 'read'
realliteral ::= "[0-9]+\.[0-9]*"
return ::= 'return'
semicolon ::= ';'
size ::= 'size'
stringliteral ::= "\"([^\"\\\\]|\\\\.)*\""
then ::= 'then'
times ::= '*'
int ::= 'integer'
real ::= 'real'
str ::= 'string'
var ::= 'var'
while ::= 'while'
write ::= 'writeln'

whitespace ::= _comment | _ws | _epsilon
_epsilon ::= ""
_comment ::= "\{\*.*?\*\}"
_ws ::= "( |\n)+"
